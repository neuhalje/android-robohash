buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.2.3'
        classpath 'org.codehaus.groovy:gradle-groovy-android-plugin:0.3.6'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'
    }
}
allprojects {
    repositories {
        mavenCentral()
        jcenter()
    }
}
apply plugin: 'com.android.library'
apply plugin: 'groovyx.grooid.groovy-android'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

task wrapper(type: Wrapper) {
    gradleVersion = '2.7'
}


android {
    compileSdkVersion 'android-17'
    buildToolsVersion '23.0.0'

    buildTypes {
        release {
            minifyEnabled false
            proguardFile getDefaultProguardFile('proguard-android.txt')
        }
    }

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }
}

dependencies {
    testCompile "org.codehaus.groovy:groovy:2.4.4"
    testCompile 'org.robospock:robospock:0.8.0'
}


def siteUrl = 'https://github.com/neuhalje/android-robohash'      // Homepage URL of the library
def gitUrl = 'https://github.com/neuhalje/android-robohash.git'   // Git repository URL

group = "name.neuhalfen.projects.android"                                        // Maven Group ID for the artifact
version = "1.0.3"


install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                
                // Add your description here
                name 'Cute litte robots for your android apps!'
                url siteUrl
                
                // Set your license
                licenses {
                    license {
                        name 'MIT License, Version 2.0'
                        url 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id 'neuhalje'
                        name 'Jens Neuhalfen'
                        email 'oss@neuhalfen.name'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl

                }
            }
        }
    }
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "robohash"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["MIT"]
        publish = true
    }
}
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}
